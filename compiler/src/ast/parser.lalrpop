use crate::ast::{ast, lex, error, parse::utils};
use ast::{Prog, Decl, Expr, RawExpr, Type, RawType, Pattern, RawPattern, Ident};


grammar<'a>;

// Using tokens from Logos
extern {
    type Location = usize;
    type Error = error::LexError;
    enum lex::Token<'a> {
        "."         => lex::Token::Dot,
        ","         => lex::Token::Comma,
        ":"         => lex::Token::Colon,
        "="         => lex::Token::Equal,
        "("         => lex::Token::LParen,
        ")"         => lex::Token::RParen,
        "["         => lex::Token::LBrack,
        "]"         => lex::Token::RBrack,
        "_"         => lex::Token::Underscore,
        "->"        => lex::Token::Arrow,
        "*"         => lex::Token::Mul,
        "infix6"    => lex::Token::Infix6(<&'a str>),
        "infix4"    => lex::Token::Infix4(<&'a str>),
        "infix3"    => lex::Token::Infix3(<&'a str>),
        "eid"       => lex::Token::ExpId(<&'a str>),
        "tid"       => lex::Token::TypId(<&'a str>),
        "intLit"    => lex::Token::IntLit(<i64>),
        "boolLit"   => lex::Token::BoolLit(<bool>),
        "unitLit"   => lex::Token::UnitLit,
        "if"        => lex::Token::If,
        "then"      => lex::Token::Then,
        "else"      => lex::Token::Else,
        "let"       => lex::Token::Let,
	"fix"       => lex::Token::Fix,
	"and"       => lex::Token::And,
        "in"        => lex::Token::In,
        "any"       => lex::Token::Any,
        "lambda"    => lex::Token::Lambda,
        "forall"    => lex::Token::Forall,
        "Int"       => lex::Token::TInt,
        "Bool"      => lex::Token::TBool,
        "Unit"      => lex::Token::TUnit
    }
}

pub Prog: Prog = {
    <ds: Decl+> => {
        let mut prog = Prog::new();
        for d in ds {
            prog.order.push(d.id.to_owned());
            prog.declarations.insert(d.id.to_owned(), d);
        }
        prog
    }
}

pub Decl: ast::Decl = {
    "let" <v: "eid"> ":" <t: TypExpr> "=" <e: ValExpr> =>
        Decl{ id: v.to_owned(), sig: t, body: e }
}

//////////////////////////////////////////////////
///////////////////// EXPR ///////////////////////
//////////////////////////////////////////////////

pub ValExpr: Expr = {
    <e: ValExpr2> => e,
    <e: ValExprIf> => e,
    <e: ValExprLet> => e,
    // <e: ValExprMatch> => e
}

ValExprIf: Expr = {
    <l: @L> "if" <e1: ValExpr> "then" <e2: ValExpr> "else" <e3: ValExpr> <r: @R> =>
        Expr {
            expr: RawExpr::If{
                cond: Box::new(e1),
                branch_t: Box::new(e2),
                branch_f: Box::new(e3)
            },
	    span: Some((l, r))
        }
}

ValExprLet: Expr = {
    <l: @L> "let" <pat: Pattern> "=" <e1: ValExpr> "in" <e2: ValExpr> <r: @R> =>
        Expr {
	    expr: RawExpr::Let{
	        pat, exp: Box::new(e1), body: Box::new(e2)
	    },
	    span: Some((l, r))
	},
    <l: @L> "fix" <fs: Sep<Fixhelp, "and">> "in" <e: ValExpr> <r: @R> => {
        Expr {
	    expr: RawExpr::Fix{
	        funcs: fs, body: Box::new(e)
	    },
	    span: Some((l, r))
	}
    }
}

// ValExprMatch: Expr = {
//   <l: @L> "match" <e: ValExpr> "with" <c: Pattern> "=>" <b: ValExpr> "end" <r: @R> =>
//        Expr {
//	    expr: RawExpr::Match{
//	        exp: Box::new(e), clause: (c, Box::new(b))
//            },
//	    span: Some((l, r))
//	}
// }


///////////// Precedence babyyyy /////////////

// Expressions that start with keywords like "let", "if", "lambda"
// ValExprKeyword: Expr = {
// }

// Lambda and any. Expression body extends as far as possible
ValExpr2: Expr = {
    // For the one-argument case, no parentheses are needed
    <l: @L> "lambda" <v: EIdent> ":" <t: TypExpr> "." <e: ValExpr> <r: @R> => Expr {
            expr: RawExpr::Lambda{ arg: (v, t), body: Box::new(e) },
	    span: Some((l, r))
        },
    <l: @L> "lambda" <args: Argument*> <last: Argument> "." <e: ValExpr> <r: @R> => {
    	    let inner = Expr {
    	    	 expr: RawExpr::Lambda{ arg: last, body: Box::new(e) },
    	    	 span: Some((l, r))
    	    };
	    args.iter().rev().fold(inner, |acc, ele| Expr {
                expr: RawExpr::Lambda{ arg: ele.clone(), body: Box::new(acc) },
                span: Some((l, r))
            })
	},
    <l: @L> "any" <ids: TIdent+> "." <e: ValExpr> <r: @R> => {
            let initial = Expr {
                expr: RawExpr::Any{ arg: ids[0].clone(), body: Box::new(e) },
                span: Some((l, r))
            };
	    ids[1..].iter().fold(initial, |acc, ele| Expr {
                expr: RawExpr::Any{ arg: ele.clone(), body: Box::new(acc) },
                span: Some((l, r))
            })
	},
    <e: ValExpr3> => e
}

// Binops with & |, precedence 3, left assoc
ValExpr3: Expr = {
    <l: @L> <e1: ValExpr3> <o: "infix3"> <e2:ValExpr4> <r: @R> =>
        Expr {
            expr: utils::make_binop(e1, o, e2),
	    span: Some((l, r))
	},
    <e: ValExpr4> => e
}

// Binops with < > == !=, precedence 4, left assoc
ValExpr4: Expr = {
    <l: @L> <e1: ValExpr4> <o: "infix4"> <e2: ValExpr6> <r: @R> =>
        Expr {
            expr: utils::make_binop(e1, o, e2),
	    span: Some((l, r))
	},
    <e: ValExpr6> => e
}

// Binops with + -, precedence 6, left assoc
ValExpr6: Expr = {
    <l: @L> <e1: ValExpr6> <o: "infix6"> <e2: ValExpr7> <r: @R> =>
        Expr {
            expr: utils::make_binop(e1, o, e2),
	    span: Some((l, r))
	},
    <e: ValExpr7> => e
}

// Binops with *, precedence 7, left assoc
ValExpr7: Expr = {
    <l: @L> <e1: ValExpr7> "*" <e2: ValExpr8> <r: @R> =>
        Expr {
            expr: utils::make_binop(e1, "*", e2),
	    span: Some((l, r))
	},
    <e: ValExpr8> => e
}

// Application, precedence 8, left assoc
ValExpr8: Expr = {
    // Expression application
    <l: @L> <e1: ValExpr8> <e2: ValExprAtom> <r: @R> =>
        Expr {
            expr: RawExpr::EApp{ exp: Box::new(e1), arg: Box::new(e2) },
	    span: Some((l, r))
	},
    // Type application
    <l: @L> <e: ValExpr8> "[" <t: TypExpr> "]" <r: @R> =>
        Expr {
            expr: RawExpr::TApp{ exp: Box::new(e), arg: t },
	    span: Some((l, r))
	},
    <e: ValExprAtom> => e
}

// Atomic expressions have no ambiguities. Highest precedence
ValExprAtom: Expr = {
    <l: @L> <z: "intLit"> <r: @R> =>
        Expr {
	    expr: RawExpr::Con{ val: ast::Constant::Integer(z) },
	    span: Some((l, r))
	},
    <l: @L> <b: "boolLit"> <r: @R> =>
        Expr {
	    expr: RawExpr::Con{ val: ast::Constant::Boolean(b) },
	    span: Some((l, r))
	},
    <l: @L> "unitLit" <r: @R> =>
        Expr {
	    expr: RawExpr::Con{ val: ast::Constant::Null },
	    span: Some((l, r))
	},
    <l: @L> <v: "eid"> <r: @R> =>
        Expr {
	    expr: RawExpr::Var{ id: v.to_owned() },
	    span: Some((l, r))
	},
    <l: @L> <es: Paren<SepMulti<ValExpr, ",">>> <r: @R> =>
        Expr {
	    expr: RawExpr::Tuple{ entries: es },
	    span: Some((l, r))
	},
    <e: Paren<ValExpr>> => e,
}

//////////////////////////////////////////////////
///////////////////// TYPE ///////////////////////
//////////////////////////////////////////////////

pub TypExpr: Type = {
    <t: TypExprForall> => t
}

TypExprForall: Type = {
    <l: @L> "forall" <ti: TIdent> "." <te: TypExpr> <r: @R> =>
	Type { typ: RawType::Forall(ti, Box::new(te)), span: Some((l, r)) },
    <t: TypExprArrow> => t
}

TypExprArrow: Type = {
    <l: @L> <t1: TypExprProd> "->" <t2: TypExprArrow> <r: @R> =>
        Type { typ: RawType::Arrow(Box::new(t1), Box::new(t2)), span: Some((l, r)) },
    <t: TypExprProd> => t
}

TypExprProd: Type = {
    <l: @L> <ts: SepMulti<TypExprAtom, "*">> <r: @R> =>
        Type { typ: RawType::Prod(ts), span: Some((l, r)) },
    <t: TypExprAtom> => t
}

TypExprAtom: Type = {
    <l: @L> "Int" <r: @R> =>
        Type { typ: RawType::Int, span: Some((l, r)) },
    <l: @L> "Bool" <r: @R> =>
        Type { typ: RawType::Bool, span: Some((l, r)) },
    <l: @L> "Unit" <r: @R> =>
        Type { typ: RawType::Unit, span: Some((l, r)) },
    <l: @L> <t: "tid"> <r: @R> =>
        Type { typ: RawType::TVar(t.to_owned()), span: Some((l, r)) },
    <t: Paren<TypExpr>> => t
}

// Argument
Argument: (Ident, Type) = {
    "(" <v: EIdent> ":" <t: TypExpr> ")" => (v, t)
}

// Help for fixpoint
Fixhelp: (Ident, Ident, Type, Type, Expr) = {
    <f: EIdent> "=" "lambda" "(" <v: EIdent> ":" <t1: TypExpr> ")" "->" <t2: TypExpr> "." <e: ValExpr> => (f, v, t1, t2, e)
}

// Pattern
Pattern: Pattern = {
    <l: @L> <multi: Paren<SepMulti<Pattern, ",">>> <r: @R> =>
        Pattern {
	    pat: RawPattern::Tuple(multi),
	    span: Some((l, r))
	},
    <l: @L> <v: EIdent> ":" <t: TypExpr> <r: @R> =>
       Pattern {
	    pat: RawPattern::Binding(v, t),
	    span: Some((l, r))
	},
    <l: @L> "_" ":" <t: TypExpr> <r: @R> =>
        Pattern {
	    pat: RawPattern::Wildcard(t),
	    span: Some((l, r))
	},
}

// Identifiers
EIdent: Ident = {
    <l: @L> <v: "eid"> <r: @R> =>
        Ident { name: v.to_owned(), span: Some((l, r)) }
}

TIdent: Ident = {
    <l: @L> <t: "tid"> <r: @R> =>
        Ident { name: t.to_owned(), span: Some((l, r)) }
}

// Binops
Binop: &'a str = {
    <op: "infix3"> => op,
    <op: "infix4"> => op,
    <op: "infix6"> => op
}

///////////// HELPERS //////////////
// One or more T's separated by S's
Sep<T, S>: Vec<T> = {
    <sep: SepMulti<T, S>> => sep,
    <atom: T> => vec![atom]
}

// Two or more T's separated by S's
SepMulti<T, S>: Vec<T> = {
    <l: (<T> S)+> <r: T> =>
        { let mut l = l; l.push(r); l }
}

// Surrounded by parentheses
Paren<T>: T = {
    "(" <t: T> ")" => t
}

//////////// END HELPERS /////////////